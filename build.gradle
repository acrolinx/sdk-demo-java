plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.diffplug.spotless' version '6.25.0'
}

dependencies {
    implementation 'com.acrolinx.client:sdk:1.0.22'
    implementation 'org.slf4j:slf4j-api:2.0.12'

    runtimeOnly 'ch.qos.logback:logback-classic:1.5.0'
}

repositories {
    mavenCentral()
}

group = 'com.acrolinx.client'
version = '0.1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

sonar {
    properties {
        property "sonar.projectKey", "acrolinx_sdk-demo-java"
        property "sonar.organization", "acrolinx"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR Acrolinx GmbH */'
    }
    format 'misc', {
        target '.gitignore', '*.gradle', '*.md', '*.properties'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.register('SimpleStringCheckDemo', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.acrolinx.client.demo.SimpleStringCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('DocxCheckDemo', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.acrolinx.client.demo.DocxCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}
