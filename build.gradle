plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.diffplug.spotless' version '6.13.0'
}

dependencies {
    implementation 'com.acrolinx.client:sdk:1.0.22'
    implementation 'org.slf4j:slf4j-api:2.0.10'

    runtimeOnly 'ch.qos.logback:logback-classic:1.4.14'
}

repositories {
    mavenCentral()
}

group = 'com.acrolinx.client'
version = '0.1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

sonar {
    properties {
        property "sonar.projectKey", "acrolinx_sdk-demo-java"
        property "sonar.organization", "acrolinx"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR Acrolinx GmbH */'
    }
    format 'misc', {
        target '.gitignore', '*.gradle', '*.md', '*.properties'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task(SimpleStringCheckDemo, dependsOn: 'classes', type: JavaExec) {
    mainClass = 'com.acrolinx.client.demo.SimpleStringCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}

task(DocxCheckDemo, dependsOn: 'classes', type: JavaExec) {
    mainClass = 'com.acrolinx.client.demo.DocxCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}
