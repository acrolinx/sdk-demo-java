plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'com.diffplug.spotless' version '6.25.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.acrolinx.client:sdk:1.0.22'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    runtimeOnly 'ch.qos.logback:logback-classic:1.5.10'
}

group = 'com.acrolinx.client'
version = '0.1.0-SNAPSHOT'

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'acrolinx'
        property 'sonar.projectKey', 'acrolinx_sdk-demo-java'
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR Acrolinx GmbH */'
    }
    format 'misc', {
        target '.gitignore', '*.gradle', '*.md', '*.properties'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.register('DocxCheckDemo', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.acrolinx.client.demo.DocxCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('SimpleStringCheckDemo', JavaExec) {
    dependsOn 'classes'
    mainClass = 'com.acrolinx.client.demo.SimpleStringCheckDemo'
    classpath = sourceSets.main.runtimeClasspath
}
